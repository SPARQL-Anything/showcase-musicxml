PREFIX ex: <http://example.org/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
# This query constructs a knowledge graph of the 3-grams from the given melody.
CONSTRUCT
{
    ?entity a rdf:Seq;
       rdf:_1 ?note ;
       rdf:_2 ?note1 ;
       rdf:_3 ?note2 .
}
WHERE 
{	
    BIND (IRI(CONCAT("x-sparql-anything:media-type=text/csv,location=", ?_filePath)) AS ?fx)
    SERVICE ?fx {
        #step,octave,type,duration,noteCount,measureCount,partCount
        #G,4,quarter,10080,2,1,6
        #B,-1,quarter,10080,1,2,6
        #B,4,quarter,10080,2,2,6
        fx:properties fx:csv.headers true. 
    [] a fx:root;
       ?mProp [ xyz:step ?note ] ;
       ?mProp1 [ xyz:step ?note1 ] ;
       ?mProp2 [ xyz:step ?note2 ] ;
       FILTER ( fx:previous(?mProp1) = 
       	  ?mProp && fx:previous(?mProp2) = ?mProp1)
    }
    BIND((fx:serial("trigram")) AS ?trigram) .
    BIND(fx:entity(ex:, ?trigram) AS ?entity) .
}